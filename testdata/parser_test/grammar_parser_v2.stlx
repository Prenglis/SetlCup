// Read a grammar from the file f, parse the grammar, and return it.
readGrammar := procedure(f) {
    gs := join(readFile(f), "\n");
    tl := tokenizeString(gs);
    return parseGrammar(tl);
};

readGrammarFromString := procedure(grammar_string){
    // gs := join(grammar_string, "\n");
    // print(grammar_string);
    tl := tokenizeString(grammar_string);
    return parseGrammar(tl);
};
// This procedure takes a token list tl and tries to interpret this list
parseGrammar := procedure(tl) {
    [rule, rl] := parseRule(tl);
    ruleList := [ rule ];
    while (#rl >= 1) {
        [rule, rl] := parseRule(rl);
        ruleList += [ rule ];
    }
    return collectSimpleRules(ruleList);
};

collectSimpleRules := procedure(ruleList) {
    rules     := [];
    variables := {};
    for ([v, bodyList] in ruleList) {
        variables += { v };
        for (body in bodyList) {
            rules += [ [v, body[..-2], body[-1]] ];
        }
    }
    start := rules[1][1];
    return [rules, variables, start];
};

parseRule := procedure(tl) {
    [head, rl] := [args(tl[1])[1], tl[2..]];
    assert(fct(tl[1]) == "Var", "parseRule($tl$)");
    assert(rl[1] == "::=", "parseRule($tl$)");
    [body, rl] := parseBody(rl[2..]);
    bodyList := [ body ];
    while (#rl >= 1 && rl[1] == "|") {
        [body, rl] := parseBody(rl[2..]);        
        // user_code := body[-1];
        // body := body[1..-2];
        bodyList += [ body ];
    }
    assert(rl[1] == ";", "parseRule($tl$), rl = $rl$");
    return [ [head, bodyList], rl[2..]];
};

parseBody := procedure(tl) {
    itemList := [];
    while (#tl >= 1 && !(tl[1] in [ "|", ";" ])) {
        [item, tl] := parseItem(tl);
        itemList += [ item ];
    }
    if(fct(itemList[-1]) != "UserCode")
    {
        itemList += [ UserCode("") ];
    }
    return [itemList, tl];
};

parseItem := procedure(tl) {
    match (tl) {
        case [ Var(var_name, var_id) | rl] : return [ Var(var_name, var_id),     rl];
        case [ Token(token_name, token_id) | rl] : return [ Token(token_name, token_id),   rl];
        case [ UserCode(u) | rl]: return [UserCode(u), rl];
        default : abort("parse error in parseItem($tl$)");
    }
};

// This procedure partitions the string s into a list of tokens.
// It recognizes numbers, the operator symbols "+", "-", "*", "/", "**"
// and the parentheses "(" and ")".
tokenizeString := procedure(s) {
    tokenList := [];
    state := "INITIAL";
    scan (s){
        regex '::=|\||;'              as [ o ] | state == "INITIAL": 
            tokenList += [ o        ];
        regex '[a-z][a-zA-Z_0-9]*(:[a-z][a-zA-Z_0-9]*)?' as [ v ] | state == "INITIAL": 
            print("v : $v$");
            if(matches(v, ".*:.+"))
            {
               [var_name, var_id] := split(v, ":"); 
            }
            else
            {
                 [var_name, var_id] := [v, ""];
            }
            tokenList += [ Var(var_name, var_id) ] ;
          // regex '[a-z][a-zA-Z_0-9]*' as [ var_name ] | state == "INITIAL": 
              // var_id := ""; 
              // tokenList += [ Var(var_name, var_id) ];
        regex '[A-Z][A-Z_0-9]*(:[a-z][a-zA-Z_0-9]*)?'    as [ t ] | state == "INITIAL": 
            if(matches(t, ".*:.+"))
            {
                 [token_name, token_id] := split(t, ":");
            }
            else
            {
                 [token_name, token_id] := [t, ""];
            }
            tokenList += [ Token(token_name, token_id) ];
        regex '\{:'  | state == "INITIAL": 
            state := "CODESEG";
        regex '.*:\}' as [ user_code ] | state == "CODESEG" : 
            state := "INITIAL"; 
            user_code := replaceFirst(user_code, "[\\s]*:}\$", ""); 
            tokenList += [ UserCode(user_code)]; 
        regex '''[^'']*'''         as [ l ] | state == "INITIAL": 
            tokenList += [ Token(l, "") ];
        regex '[ \t\v\n\r]+'               | state == "INITIAL": 
            // skip
        regex '.|\n'               as [ c ] | state == "INITIAL": 
            print("tokenList =$tokenList$");
           print("ERROR : tokenizeString: $c$");
    }
    return tokenList;
};

grammar2String := procedure(ruleList) {
    result := "";
    for ([v, bl] in ruleList) {
        bh := bl[1]; br := bl[2..];
        result += v + ":\n      " + itemList2String(bh) + "\n";
        result += bodyList2String(br);
    }
    return result;
};

bodyList2String := procedure(l) {    
    match (l) {
        case []: 
             return "    ;\n\n";
        case [bh| bl]: 
             return "    | " + itemList2String(bh) + "\n" + bodyList2String(bl);
    }
};

itemList2String := procedure(l) {
    match (l) {
        case []: 
             return "";
        case [x]: 
             return item2String(x);
        case [h | t]: 
             return item2String(h) + " " + itemList2String(t);
    }
};

item2String := procedure(i) {
    match (i) {
        case Var(var_name, var_id):   
            if(var_id != "")
                {
                    return var_name + ":" + var_id;
                }
            else
            {
                return var_name;
            }
        case Token(token_name, token_id): 
            if(token_id != "")
                {
                    return token_name + ":" + token_id;
                }
            else
            {
                return token_name;
            }
        case UserCode(u): return "{: " + u + " :}";
        default:       abort("item2String($i$)");
    }
};

testParser := procedure(f) {
    for ([v, il] in readGrammar(f)[1]) {
         print("$v$ -> $itemList2String(il)$");
    }
};

// testParser("expr.g");





/* Combine a list of grammar symbols, i.e. of variables and tokens into a 
   string.
 */
list2str := procedure(l) {
    result := "";
    for (x in l) {
        match (x) {
            case Var(var_name, var_id): 
                if(var_id != "") 
                {
                    result += " " + var_name + ":" + var_id;
                }
                else
                {
                     result += " " + var_name;
                }
            case Token(token_name, token_id) : 
                if(token_id != "") 
                {
                    result += " " + token_name + ":" + token_id;
                }
                else
                {
                     result += " " + token_name;
                }
            case UserCode(u): result += "{: " + u + " :}";
            default: print("Error, $x$ can not be parsed");
        }
    }
    return result;
};

escapeDollar := procedure(t) {
    if (t == '$') { 
        return '\$'; 
    }
    return t; 
};

escapeDollar2 := procedure(t) {
    if (t == '$') { 
        return "'\$'"; 
    }
    if (t[1] != "'") {
        return '"' + t + '"'; 
    }
    return t;
};

/* The objects of this class represent extended marked rules of the form
   variable -> alpha * beta : L user_code.
*/
class markedRule(variable, alpha, beta, followSet, user_code) {
    this.mVariable  := variable;
    this.mAlpha     := alpha;
    this.mBeta      := beta;
    this.mFollowSet := followSet;
    this.mUser_Code := user_code;

    // check whether the * is at the end of of the rule
    isComplete := procedure() { return mBeta == []; };
    nextVar    := procedure() {
        if (#mBeta > 0) {
            match (mBeta[1]) {
                case Var(var_name, var_id): return var_name;
            }
        }
    };
    symbolAfterDot := procedure() {
        if (#mBeta > 0) { return mBeta[1]; }
    };
    restAfterVariable := procedure() {
        if (#mBeta > 1) { return mBeta[2..]; }
        return [];
    };
    moveDot := procedure() {
        return markedRule(mVariable, mAlpha + [mBeta[1]], mBeta[2..], mFollowSet, mUser_Code);
    };  
    // turn a marked rule into a grammar rule
    toRule := procedure() {
        return grammarRule(mVariable, mAlpha + mBeta, mUser_Code);
    };
    toParserRule := procedure(){
        [args, mUser_Code] := extractArgList(mAlpha + mBeta);
        return Rule(mVariable, args, mUser_Code);
    };
    f_str := procedure() {
                
        rule :=  "$mVariable$";
        rule += " ->" ;
        rule += list2str(mAlpha) + " (*)";
        rule +=  list2str(mBeta) + ": {";
        n := 1;
        for (t in mFollowSet) {
            rule += escapeDollar(t);
            if (n < #mFollowSet) {
                rule += ", ";
            }
        }
        rule += "}";
        return rule;
    };

  static {
    // take a core rule and attach a follow set
    createRule := procedure(cr, followSet) {
        return markedRule(cr.mVariable, cr.mAlpha, cr.mBeta, followSet, cr.user_code);
    };  
  }
}

// A coreRule is a markedRule without a follow set.
class coreRule(mr) {
    this.mVariable  := mr.mVariable;
    this.mAlpha     := mr.mAlpha;
    this.mBeta      := mr.mBeta;
    this.mUser_Code := mr.mUser_Code;

    static {
        equals := procedure(that) {
            return this.mVariable == that.mVariable &&
                   this.mAlpha    == that.mAlpha    &&
                   this.mBeta     == that.mBeta     &&
                   this.mUser_Code == that.mUser_Code;
        };
    }
}

// class parserRule(variable, gamma){
//     this.mVariable := variable;
//     this.mGamma    := gamma;
//     f_str := procedure() {
//         rule   := Rule(mVariable, extractArgList(mGamma));
//         return rule;
//     };
// }

/* This class represents a grammar rule. */
class grammarRule(variable, gamma, user_code) {
    this.mVariable := variable;
    this.mGamma    := gamma;
    this.mUser_Code := user_code;
    f_str := procedure() {
        return mVariable + " ->" + list2str(mGamma);
    };
}

// assign the empty set to all keys
initializeMap := procedure(keys) {
    return { [k, {}] : k in keys };
};

/* This class represents a context-free grammar */
class grammar(rules, variables, start) {
    this.mRules       := rules;
    this.mVariables   := variables;
    this.mStart       := start;
    this.mRules       += { ["S", [Var(mStart, "")], UserCode("")] }; // augment grammar
    this.mVariables   += { "S" };
    this.mFirst       := initializeMap(mVariables);
    this.mFollow      := initializeMap(mVariables);
    this.mTokens      := {};
    this.mStates      := {};
    this.mStateNames  := {};
    this.mRuleNames   := {};
    this.mConflicts   := false;

  static {
    assignRuleNames := procedure() {
        counter := 0;
        print("rulesMap := {};");
        for ([head, body, user_code] in mRules) {
        [args, user_code] := extractArgList(body);
        rule                  := Rule(head, args, user_code);
            ruleName              := "r" + counter;
            this.mRuleNames[rule] := ruleName;
            print("$ruleName$ := $rule$;");
            print("rulesMap[\"$ruleName$\"] := $rule$;");
            counter += 1;
        }
        print("\n");
    };
    // check, whether this grammar is an LR-grammar  
    checkLR := procedure() {
        computeTokens();
        computeFirst();
        assignRuleNames();
        computeAllStates();
        computeActionTable();
        computeGotoTable();
    };
    // collect all tokens  
    computeTokens := procedure() {
        for ([_, body, _] in mRules, x in body) {
            match (x) {
                case Token(token_name, token_id): this.mTokens += { x };
            }
        }
    };
    startRule := procedure() {
        return markedRule("S", [], [ Var(mStart, "") ], { '$' }, UserCode(""));
    };
    firstList := procedure(alpha) {
        match (alpha) {
             case []:               return { "" };
             case [ Var(var_name, var_id)   | r ]: return epsAppend(mFirst[var_name], firstList(r));
             case [ Token(token_name, token_id) | r ]: return { token_name };
        }
    };
    computeFirst := procedure() {
        change := true;
        while (change) {
            change := false;    
            for ([a, body, _] in mRules) {
                firstBody := firstList(body);
                if (!(firstBody <= mFirst[a])) { 
                    change := true;
                    this.mFirst[a] += firstBody;
                }
            }
        }
    };
    epsClosure := procedure(markedRules) {
        allRules := markedRules;
        newRules := markedRules;
        while (true) {
            moreRules := {};
            for (mr in newRules) {
                c := mr.nextVar();
                if (c == om) { continue; }
                for ([head, alpha, code_section] in mRules | c == head) {
                    followSet := epsAppend(firstList(mr.restAfterVariable()), mr.mFollowSet);
                    moreRules += { markedRule(head, [], alpha, followSet, code_section) };
                }
            }
            if (moreRules <= allRules) {
                return combineRules(allRules);
            }
            newRules := moreRules - allRules;
            allRules += newRules;
        }
    };
    // combine those marked rules in the given set that have the same core
    combineRules := procedure(markedRules) {
        cores   := { coreRule(mr) : mr in markedRules };
        combine := procedure(rules, cr) {
            return +/ { mr.mFollowSet : mr in rules | cr == coreRule(mr) };
        };
        return { markedRule.createRule(cr, combine(markedRules, cr)) : cr in cores };
    };
    goto := procedure(markedRules, x) {
        result := {};
        for (mr in markedRules | mr.symbolAfterDot() == x) {
            result += { mr.moveDot() };
        }
        return epsClosure(result);
    };
    computeAllStates := procedure() {
        this.mStates := newStates := { epsClosure({ startRule() }) };
        while (true) {
            moreStates := {};
            for (ruleSet in newStates) {
                for (mr in ruleSet | !mr.isComplete()) {
                    x := mr.symbolAfterDot();
                    moreStates += { goto(ruleSet, x) };
                }
            }
            if (moreStates <= mStates) {
                break;
            }
            newStates := moreStates - mStates;
            this.mStates += newStates;
        }
        print("stateTable := {};");
        counter := 0;
        for (state in mStates) {
            this.mStateNames[state] := "s" + counter;
            print("stateTable[\"s$counter$\"] := $state2String(state)$;");
            counter += 1;
        }
        print("");
    };
    state2String := procedure(state) {
        result := '"{ ';
        n := 1;
        for (mr in state) {
            result += mr;
            if (n < #state) {
                result += ", ";
            }
        }
        result += ' }"';
        return result;
    };
    computeActionTable := procedure() {
        print("actionTable := {};");
        for (state in mStates) {
            actionTable := {};
            // compute shift actions
            for (token in mTokens) {
                stateName := mStateNames[state];
                newState  := goto(state, token);
                if (newState != {}) {
                    newName := mStateNames[newState];
                    t       := escapeDollar2(extractArg(token));
                    actionTable[t] := Shift(newName);
                    print("actionTable[\"$stateName$\", $t$] := $Shift(newName)$;");
                }
            }
            // compute reduce actions
            for (mr in state | mr.isComplete()) {
                for (t in mr.mFollowSet) {
                    t := escapeDollar2(t);                    
                    // print("mRuleNames: $this.mRuleNames$");
                    // print("mr: $mr$");
                    // print("mr.toParserRule(): $mr.toParserRule()$");
                    parserRuleString := mr.toParserRule();
                    // print("parserRuleString: $parserRuleString$");
                    ruleName := this.mRuleNames[parserRuleString];
                    // print("parserRuleString == Rule(\"fact\", [\"LPAREN\", \"expr\", \"RPAREN\"]) : $parserRuleString$ == $Rule(\"fact\", [\"LPAREN\", \"expr\", \"RPAREN\"])$ : " + parserRuleString ==Rule("fact", ["LPAREN", "expr", "RPAREN"]));
                    // print("this.mRuleNames[Rule(\"fact\", [\"LPAREN\", \"expr\", \"RPAREN\"])] = "+ this.mRuleNames[Rule("fact", ["LPAREN", "expr", "RPAREN"])]);
                    // print("this.mRuleNames[mr.toParserRule()] := this.mRuleNames[$parserRuleString$]");
                    // print("mRuleName: $ruleName$");
                    action1  := actionTable[t];
                    // action2  := Reduce(mr.toRule() + "");
                    action2 := Reduce(ruleName);
                    if (action1 == om) {
                        // print("args(parserRuleString) : $args(parserRuleString)$");
                        // print("[\"S\", [Var(mStart)]] : $[\"S\", [mStart]]$");
                        if(args(parserRuleString) == ["S", [mStart]])
                        {
                            action2 := Accept();
                        }
                        actionTable[t] := action2;
                        // print("mSTart: $mStart$");
                        // print("mRules: $mRules$");

                        print("actionTable[\"$stateName$\", $t$] := $action2$;");
                    } else if (action1 != action2) {
                        this.mConflicts := true;
                        print("//");
                        print("// conflict:");
                        print("// action[\"$stateName$\", $t$] := $action1$;");                        
                        print("// action[\"$stateName$\", $t$] := $action2$;");
                        print("//");
                    }
                }
            }
            print("");
        }
    };
    computeGotoTable := procedure() {
        print("gotoTable := {};");
        for (state in mStates, var in mVariables) {
            newState := goto(state, Var(var_name, var_id));
            if (newState != {}) {
                stateName := mStateNames[state];
                newName   := mStateNames[newState];
                print("gotoTable[\"$stateName$\", \"$var$\"] := \"$newName$\";");
            }
        }
    };
    // auxilliary procedures
    f_str := procedure() {
        result := "";
        for ([head, body] in mRules) {
            result += "$head$ ->";
            for (i in body) {
                result += " " + extractArg(i);
            }
            result += " ;\n";
        }                  
        return result;
    };
    epsAppend := procedure(s, t) {
        if ("" in s) {
            if ("" in t) {
                return s + t;
            } 
            return s + t - { "" };
        }
        return s;
    };
  }  
}
    extractArgList := procedure(l) {
        args := [extractArg(t) : t in l];
        user_code := args[-1];        
        if(!matches(user_code, '\{:.*:\}'))
        {
            args[#args+1] := "{: :}";
        }
        return [args[..-2], args[-1]];
    };
    extractArg := procedure(x) {
        match (x) {
            case Var(var_name, var_id)   : 
                if(var_id!= "")
                {
                    return var_name + ":" + var_id;
                }
                else
                {
                    return var_name;
                }
                    
            case Token(token_name, token_id) :
                if(token_id!= "")
                {
                    return token_name + ":" + token_id;
                }
                else
                {
                    return token_name;
                }
                    
            case UserCode(u) : return "{:" + user_code + ":}";
        }
    };

testFromString := procedure(grammar_string){
    [rules, variables, start] := readGrammarFromString(grammar_string);
    // readGrammarFromString(grammar_string);
    gr := grammar(rules, variables, start);
    gr.checkLR();

};
test := procedure(file) {
    [rules, variables, start] := readGrammar(file);
    gr := grammar(rules, variables, start);
    gr.checkLR();
};

main := closure() {
    if (params != []) {
        test(params[1]);
    } else {
        print("Call this program as follows: \"setlx lr-table-generator.stlx --params \'file\'");
        print("Here file should contain the grammar.");
    }
};
